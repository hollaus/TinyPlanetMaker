apply plugin: 'com.android.application'

android {

    compileSdkVersion 23
    buildToolsVersion '25.0.0'
    defaultConfig {
        applicationId "org.hofapps.tinyplanet"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 6
        versionName "1.2.2"
    }
    // begin
    sourceSets.main {
        jni.srcDirs = [] //disable automatic nd k-build call
    }
    task ndkBuild(type: Exec) {
        workingDir file('src/main')
        commandLine getNdkBuildCmd()
    }

    task cleanNative(type: Exec, description: 'Clean JNI object files') {
        commandLine getNdkBuildCmd(),
                '-C', file('src/main/jni').absolutePath,
                'clean'
    }

    clean.dependsOn 'cleanNative'

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkBuild
    }
    //end
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
//            signingConfig signingConfigs.config2
        }
        debug {
//            signingConfig signingConfigs.config2
        }
    }
    productFlavors {
    }


}

def getNdkDir() {
    if (System.env.ANDROID_NDK_ROOT != null)
        return System.env.ANDROID_NDK_ROOT

    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def ndkdir = properties.getProperty('ndk.dir', null)
    if (ndkdir == null)
        throw new GradleException("NDK location not found. Define location with ndk.dir in the local.properties file or with an ANDROID_NDK_ROOT environment variable.")

    return ndkdir
}

def getNdkBuildCmd() {
    def ndkbuild = getNdkDir() + "/ndk-build"
    // TODO: check why we cannot include package Os:
//    if (Os.isFamily(Os.FAMILY_WINDOWS))
    ndkbuild += ".cmd"

    return ndkbuild
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:design:23.3.0'
    compile project(':openCVLibrary310')
    compile 'com.android.support.constraint:constraint-layout:1.0.1'
    compile 'com.isseiaoki:simplecropview:1.1.4'
}